plugins {
    id "dev.architectury.loom" version "1.6-SNAPSHOT" apply false
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id "me.shedaniel.unified-publishing" version "0.1.+" apply false
    id "maven-publish"
}

architectury {
    minecraft = rootProject.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"
    apply plugin: "me.shedaniel.unified-publishing"
    apply plugin: "java"

    base {
        archivesName = rootProject.archives_base_name
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.

        mavenCentral()

        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }

        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
        }

        maven {
            name = "Infinituum Mod Resources"
            url = "https://raw.githubusercontent.com/Infinituum17/ModResources/main/maven/"
        }
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    if ((project.name == "fabric" || project.name == "neoforge") && project.version != "0.0.0") {
        unifiedPublishing {
            project {
                displayName = "Fast Config API ${project.minecraft_version}-${project.name} - ${project.mod_version}"
                gameVersions = [project.minecraft_version]
                gameLoaders = [project.name]
                releaseType = "release"
                version = project.version

                def clog = new File(project.getRootDir(), "changelog.md")

                if (clog.exists()) {
                    changelog = clog.getText()
                }

                relations {
                    depends {
                        curseforge = "void-lib"
                        modrinth = "void-lib"
                    }
                }

                mainPublication remapJar

                var curseforgeToken = System.getenv("CURSEFORGE_TOKEN")
                if (curseforgeToken != null) {
                    curseforge {
                        token = curseforgeToken
                        id = "990853"
                    }
                }

                var modrinthToken = System.getenv("MODRINTH_TOKEN")
                if (modrinthToken != null) {
                    modrinth {
                        token = modrinthToken
                        id = "jhHNjn7K"
                    }
                }
            }
        }
    }
}
